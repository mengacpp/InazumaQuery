cmake_minimum_required(VERSION 3.15)

# 1. Make install dirs relocatable & follow distro conventions:
include(GNUInstallDirs)

project(inazuma LANGUAGES C VERSION 0.1)

set(CMAKE_EXPORT_COMPILE_COMMANDS ON)

set(CMAKE_C_STANDARD 99)
set(CMAKE_C_STANDARD_REQUIRED ON)
set(CMAKE_C_EXTENSIONS OFF)

file(GLOB_RECURSE HEADERS include/inazuma/*.h)
file(GLOB_RECURSE SOURCES src/*.c)

add_library(inazuma SHARED ${HEADERS} ${SOURCES})

target_compile_definitions(inazuma PRIVATE 
    INAZUMA_EXPORTS 
    INAZUMA_DATA_DIR="${CMAKE_INSTALL_FULL_DATADIR}/inazuma")
target_include_directories(inazuma
  PUBLIC
    $<BUILD_INTERFACE:${CMAKE_CURRENT_SOURCE_DIR}/include>
    $<INSTALL_INTERFACE:include>
)

# 2. Install the library target
install(
  TARGETS inazuma
  EXPORT inazumaTargets
  ARCHIVE DESTINATION ${CMAKE_INSTALL_LIBDIR}    # .a on UNIX
  LIBRARY DESTINATION ${CMAKE_INSTALL_LIBDIR}    # .so/.dylib 
  RUNTIME DESTINATION ${CMAKE_INSTALL_BINDIR}    # .exe on Windows
  INCLUDES DESTINATION ${CMAKE_INSTALL_INCLUDEDIR}
)

# 3. Install public headers
install(
  DIRECTORY ${CMAKE_CURRENT_SOURCE_DIR}/include/inazuma
  DESTINATION ${CMAKE_INSTALL_INCLUDEDIR}
  FILES_MATCHING PATTERN "*.h"
)

# 4. Export a config file so users can find_package(inazuma)
install(
  EXPORT inazumaTargets
  NAMESPACE inazuma::
  DESTINATION ${CMAKE_INSTALL_LIBDIR}/cmake/inazuma
)

# 5. write a version file
include(CMakePackageConfigHelpers)
write_basic_package_version_file(
  "${CMAKE_CURRENT_BINARY_DIR}/inazuma-config-version.cmake"
  VERSION ${PROJECT_VERSION}
  COMPATIBILITY AnyNewerVersion
)

# config

include(CMakePackageConfigHelpers)

configure_package_config_file(
  "${CMAKE_CURRENT_SOURCE_DIR}/cmake/inazuma-config.cmake.in"
  "${CMAKE_CURRENT_BINARY_DIR}/inazuma-config.cmake"
  INSTALL_DESTINATION ${CMAKE_INSTALL_LIBDIR}/cmake/inazuma
)

write_basic_package_version_file(
  "${CMAKE_CURRENT_BINARY_DIR}/inazuma-config-version.cmake"
  VERSION ${PROJECT_VERSION}
  COMPATIBILITY AnyNewerVersion
)

install(
  FILES
    "${CMAKE_CURRENT_BINARY_DIR}/inazuma-config.cmake"
    "${CMAKE_CURRENT_BINARY_DIR}/inazuma-config-version.cmake"
  DESTINATION ${CMAKE_INSTALL_LIBDIR}/cmake/inazuma
)

install(
  DIRECTORY ${CMAKE_CURRENT_SOURCE_DIR}/data
  DESTINATION ${CMAKE_INSTALL_DATADIR}/inazuma
)